plugins {
	id 'groovy-gradle-plugin'
	id 'maven-publish'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.code.gson:gson:2.12.1'
}

group = 'io.github.fourmisain'
version = '0.0.7'

gradlePlugin {
	plugins {
		glue {
			id = 'glue'
			implementationClass = 'io.github.fourmisain.glue.GluePlugin'
		}
	}
}

publishing {
	repositories {
		def GITLAB_PRIVATE_ACCESS_TOKEN = System.getenv("GITLAB_PRIVATE_ACCESS_TOKEN")
		if (GITLAB_PRIVATE_ACCESS_TOKEN == null) {
			throw new GradleException("GITLAB_PRIVATE_ACCESS_TOKEN not defined")
		} else {
			maven {
				url = "https://gitlab.com/api/v4/projects/37712942/packages/maven"
				credentials(HttpHeaderCredentials) {
					name = 'Private-Token'
					value = GITLAB_PRIVATE_ACCESS_TOKEN
				}
				authentication {
					header(HttpHeaderAuthentication)
				}
			}
		}
	}
}
